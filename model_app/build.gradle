apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

//获取项目的扩展属性
def ext = rootProject.ext
//ext为上方属性
def versions = ext.versions
def dependcy = ext.dependencies

// 创建一个名为keystorePropertiesFile的变量，并将其初始化为密钥存储库。属性文件，在rootProject文件夹中。
def keystorePropertiesFile = rootProject.file("keystore.properties")
// 初始化一个名为keystoreProperties的新属性对象。
def keystoreProperties = new Properties()
// 加载密钥存储库。属性文件到keystoreProperties对象。
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

//记录构建时间
static def buildTime() {
    return new Date().format("yyyyMMdd")
}

android {
    compileSdkVersion versions.compileSdkVersion

    defaultConfig {
        applicationId "com.dong.androidmoduledev"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode 1
        versionName "1.0"
        //dex多分包
        multiDexEnabled true
        //打包时间
        resValue "string", "build_time", buildTime()
    }

    //jdk编译版本设置
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //构建类型设置
    buildTypes {
        release {
            // 更改AndroidManifest.xml中预先定义好占位符信息
            manifestPlaceholders = [
                    app_icon : "@mipmap/ic_launcher",
                    app_label: "@string/app_name"
            ]
            // 自定义BuildConfig字段
            // 不显示Log
            buildConfigField "boolean", "LEO_DEBUG", "false"
            // 是否zip对齐
            zipAlignEnabled true
            // 缩减resource文件
            shrinkResources true
            // Proguard 混淆设置
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 签名
//            signingConfig signingConfigs.release
        }

        debug {
            // 给applicationId添加后缀“.debug”，这样手机就可以支持安装多个当前app
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                    app_icon : "@mipmap/ic_launcher_round",
                    app_label: "@string/app_name_debug"
            ]
            buildConfigField "boolean", "LOG_DEBUG", "true"
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            debuggable true
//            signingConfig signingConfigs.debug
        }
    }

//    //签名信息配置
//    signingConfigs {
//        release {
//            keyAlias keystoreProperties['keyAlias']
//            keyPassword keystoreProperties['keyPassword']
//            storeFile file(keystoreProperties['storeFile'])
//            storePassword keystoreProperties['storePassword']
//        }
//        debug {
//            keyAlias keystoreProperties['keyAlias']
//            keyPassword keystoreProperties['keyPassword']
//            storeFile file(keystoreProperties['storeFile'])
//            storePassword keystoreProperties['storePassword']
//        }
//    }

    //dex多分包配置
    dexOptions {
        //增加java堆内存大小
        javaMaxHeapSize "4g"
        //关闭对Lib做 preDexing
        preDexLibraries = false
        //dex参数详见 dx --help
        additionalParameters = [
                //多分包
                '--multi-dex',
                //每个包内方法数上限
                '--set-max-idx-number=60000',
                //打包进主classes.dex的文件列表
                '--main-dex-list=' + projectDir + '/maindexlist.txt',
                //使上一句生效
                '--minimal-main-dex'
        ]
    }
}

//依赖设置
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation dependcy.kotlin_stdlib_jdk7
    implementation dependcy.multidex
    implementation dependcy.appcompat_v7
    implementation dependcy.constraint_layout
}
