if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

//获取项目的扩展属性
def ext = rootProject.ext
//ext为上方属性
def versions = ext.versions
def dependcy = ext.dependencies

kapt {
    arguments {
        arg("moduleName", project.getName())
    }
}

android {
    compileSdkVersion versions.compileSdkVersion

    defaultConfig {
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode 1
        versionName "1.0"

        // java编译对annotation的配置
        javaCompileOptions {
            annotationProcessorOptions {
                // 是否包含编译Class的路径（供系统使用）
                includeCompileClasspath = true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //构建类型设置
    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //重新设置资源指向
    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
                //组件模式下将组件使用的java目录导入进来
                java.srcDirs 'src/main/module/java', 'src/main/java'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式只保留原有的java目录
                java.srcDirs 'src/main/java'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':lib_common')

    kapt dependcy.arouter_compiler
}
